Вывод параболы

вводим символ для вывода в 1:
+++++++++++++++++++++++++++++++++++
вводим символ перевода строки в 10:
>>>>>>>>>++++++++++

переходим в 2 и вводим туда максимальное значение x(15) это будет счётчиком цикла:
<<<<<<<<
+++++++++++++++
[
    уменьшаем счётчик в 2 и увеличиваем 3 (в 3 текущий х)
    ->+
    мы в 3

    после предыдущих умножений в 4 и 5 остались числа
    убираем их
    >[-]>[-]<<

    копируем 3 в 4:
        этап 1: копируем его в 4 и 5:
        [->+>+<<]
        мы в 3
        этап 2: оно стало нулём переносим в него то; что в 5:
        >>[-<<+>>]
        мы в 5
    переходим в 3:
    <<

    мы в 3
    умножаем 3 на 4; результат будет в 9
    в комментах умножения адреса относительные
        само умножение:
        копируем первое число в 3:
            этап 1: копируем его в 3 и 4
            [->>+>+<<<]
            этап 2: оно стало нулём; переносим в него то; что в 4:
            >>>[-<<<+>>>]
            мы в 4
        переходим в 2
        <<
        копируем второе число в 4:
            этап 1: копируем его в 4 и 5
            [->>+>+<<<]
            этап 2: оно стало нулём; переносим в него то; что в 5:
            >>>[-<<<+>>>]
            мы в 5
        переходим в 4
        <
        [
            переходим в 3:
            <
            копируем копию первого числа в 3й ячейке в 5:
                этап 1: копируем его в 5 и 6
                [->>+>+<<<]
                этап 2: оно стало нулём; переносим в него то; что в 6:
                >>>[-<<<+>>>]
                мы в 6
            после этого нужно пятёрку переносить в 6 и уменьшать четвёрку

            переходим в 5:
            <
            переносим 5 в 7:
            [->>+<<]
            мы в 5
            переходим в 4 и уменьшаем её; четвёрка является управляющей переменной внешнего цикла
            <-
        ]
        мы в 4
        готово: в 7ке результат умножения
    с учётом относительных адресов мы в 6
    а результат в 9

    выводим количество символов; содержащееся в 9
    переходим в 9
    >>>
    выводим
    [-<<<<<<<<.>>>>>>>>]
    мы в 9

    переходим в 10 и выводим символ оттуда (это перевод строки)
    >.

    возвращаемся в 2:
    <<<<<<<<
]